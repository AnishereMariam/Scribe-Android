plugins {
    id 'com.android.application'
    id 'kotlin-android'
    id 'kotlin-kapt'
    id("org.jmailen.kotlinter")


}

def keystorePropertiesFile = rootProject.file("keystore.properties")
def keystoreProperties = new Properties()
if (keystorePropertiesFile.exists()) {
    keystoreProperties.load(new FileInputStream(keystorePropertiesFile))
}

android {
    compileSdk 34

    kotlinOptions {
        jvmTarget = "1.8"
    }


    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    buildFeatures {
        viewBinding = true
        buildConfig true
    }

    defaultConfig {
        applicationId "be.scri"
        minSdk 26
        targetSdk 34
        versionCode 7
        versionName "1.0.0"
        multiDexEnabled true
        setProperty("archivesBaseName", "scribe")
        vectorDrawables.useSupportLibrary = true
    }

    signingConfigs {
        if (keystorePropertiesFile.exists()) {
            release {
                keyAlias keystoreProperties['keyAlias']
                keyPassword keystoreProperties['keyPassword']
                storeFile file(keystoreProperties['storeFile'])
                storePassword keystoreProperties['storePassword']
            }
        }
    }

    buildTypes {
        debug {
            applicationIdSuffix ".debug"
        }
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            if (keystorePropertiesFile.exists()) {
                signingConfig signingConfigs.release
            }
        }
    }

    flavorDimensions "variants"
    productFlavors {
        core {}
        fdroid {}
    }

    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
    }

    lint {
        abortOnError false
        checkReleaseBuilds false
    }

    detekt {
        toolVersion = "1.23.6"
        buildUponDefaultConfig = true
        allRules = true
    }


    namespace 'be.scri'
}

dependencies {
    implementation 'androidx.appcompat:appcompat:1.7.0'
    implementation 'androidx.activity:activity-ktx:1.9.2'
    implementation 'androidx.navigation:navigation-fragment-ktx:2.8.0'
    implementation 'androidx.navigation:navigation-ui-ktx:2.8.0'
    kapt 'androidx.room:room-compiler:2.6.1'
    implementation 'androidx.room:room-runtime:2.6.1'
    annotationProcessor 'androidx.room:room-compiler:2.6.1'
    // Commons dependencies
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    implementation 'androidx.constraintlayout:constraintlayout:2.1.4'
    implementation 'androidx.documentfile:documentfile:1.0.1'
    implementation 'androidx.swiperefreshlayout:swiperefreshlayout:1.1.0'
    implementation "androidx.exifinterface:exifinterface:1.3.7"
    implementation "androidx.biometric:biometric-ktx:1.2.0-alpha05"
    implementation 'com.google.android.material:material:1.12.0'
    implementation 'androidx.recyclerview:recyclerview:1.3.2'
    implementation 'androidx.cardview:cardview:1.0.0'
    implementation "androidx.viewpager2:viewpager2:1.1.0"

    api 'joda-time:joda-time:2.10.13'
    api 'com.github.tibbi:RecyclerView-FastScroller:e7d3e150c4'
    api 'com.github.tibbi:reprint:2cb206415d'
    api 'androidx.core:core-ktx:1.13.1'
    api 'androidx.appcompat:appcompat:1.7.0'
    api 'com.google.android.material:material:1.12.0'
    api 'com.google.code.gson:gson:2.10.1'

    api 'com.github.bumptech.glide:glide:4.12.0'
    kapt 'com.github.bumptech.glide:compiler:4.12.0'
    annotationProcessor 'com.github.bumptech.glide:compiler:4.12.0'
}

task moveFromi18n {
    def locales = ['de','es','sv','en-US']

    locales.each { locale ->
        def fromDir = "src/main/assets/i18n/Scribe-i18n/values/${locale}/"
        def toDir = locale == 'en-US' ? "src/main/res/values/" : "src/main/res/values-${locale}/"
        def sourceDir = file(fromDir)

        if (sourceDir.exists()) {
            println "Preparing to move from $fromDir to $toDir"
            doLast {
                copy {
                    from fileTree(dir: fromDir)
                    into toDir
                    duplicatesStrategy = DuplicatesStrategy.INCLUDE
                }
            }
        } else {
            println "Source directory does not exist: $fromDir"
        }
    }
}

tasks.named('preBuild').configure {
    dependsOn tasks.named('moveFromi18n')
}

